plugins {
	id 'fabric-loom' version '1.1-SNAPSHOT'
	id 'maven-publish'
	id "com.modrinth.minotaur" version "2.+"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version + "-polymerport." + project.sub_version
group = project.maven_group

repositories {
	maven { url = "https://www.cursemaven.com" }
	maven { url = "https://maven.nucleoid.xyz" }
	maven { url = "https://oss.sonatype.org/content/repositories/snapshots" }
	maven { url 'https://maven.wispforest.io' }
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
}

dependencies {
	implementation 'org.jetbrains:annotations:22.0.0'

	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modRuntimeOnly "maven.modrinth:lazydfu:0.1.3"

	modCompileOnly "curse.maven:repurposed-structures-fabric-${project.rs_project_id}:${project.rs_file_id}"

	modApi "maven.modrinth:pinlib:$project.pinlib_api_version"
	modRuntimeOnly "maven.modrinth:pinlib:$project.pinlib_api_version"

	annotationProcessor modImplementation("io.wispforest:owo-lib:${project.owo_version}")
	include "io.wispforest:owo-sentinel:${project.owo_version}"

	modImplementation include("eu.pb4:sgui:${project.sgui_version}")
	modImplementation include("eu.pb4:hologram-api:${project.hologram_api_version}")
	modImplementation include("eu.pb4:polymer-common:${project.polymer_common_version}")
	modImplementation include("eu.pb4:polymer-core:${project.polymer_core_version}")
	modImplementation include("eu.pb4:polymer-resource-pack:${project.polymer_resource_pack_version}")
	modImplementation include("eu.pb4:polymer-blocks:${project.polymer_blocks_version}")
	modImplementation include("xyz.nucleoid:server-translations-api:${project.server_translations_api_version}")
	modImplementation include("me.lucko:fabric-permissions-api:${project.fabric_permissions_api_version}")
}

loom {
	accessWidenerPath = file("src/main/resources/fwaystones.accesswidener")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
	repositories {
	}
}


/*curseforge {
	if (System.getenv("CURSEFORGE")) {
		apiKey = System.getenv("CURSEFORGE")

		project {
			id = "572651"
			releaseType = "release"
			changelog = System.getenv("CHANGELOG")
			changelogType = "markdown"
			addGameVersion ((String) project.minecraft_version)
			addGameVersion "Fabric"
			mainArtifact(remapJar)

			afterEvaluate {
				uploadTask.dependsOn("remapJar")
			}
		}
	}

	options {
		forgeGradleIntegration = false
	}
}*/

if (System.getenv("MODRINTH")) {
	modrinth {
		token = System.getenv("MODRINTH")
		projectId = 'gU9Ao8K2'
		versionNumber = version
		versionType = "release"
		changelog = System.getenv("CHANGELOG")
		// On fabric, use 'remapJar' instead of 'jar'
		uploadFile = remapJar
		gameVersions = [((String) project.minecraft_version)]
		loaders = ["fabric", "quilt"]
	}

	remapJar {
		finalizedBy project.tasks.modrinth
	}
}
